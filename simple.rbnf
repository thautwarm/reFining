import std.common.[Name Space Number DoubleQuotedStr Comment]
pyimport refining.reunify.[*]
ignore [Space]

SingleQuote := R'\''
keyword cast := 'let' 'in' 'type' 'fn' 'of'

Sym ::= Name as n [':' Type as ty]
        rewrite
            r = Id(n.value)
            if ty:
                r = Annotate(r, ty)
            r

Str ::= DoubleQuotedStr+ as seq
        rewrite
            Const(''.join(e.value for e in seq))

Num ::= Number as num
        rewrite
            Const(int(num.value))

Let ::=
    | 'let' Sym as sym '=' Expr as value 'in' Expr as do
    rewrite
        Let(sym, value, do)

Expr ::= | Let as v
         | App as v
         | TypeDef as v
         rewrite v

App  ::= Atom+ as seq
         rewrite
            head, *tail = seq
            if tail:
                for e in tail:
                    head = App(head, e)
            head

Lam ::= 'fn' Sym as sym '->' Expr as ret
         rewrite
            Lam(sym, ret)

Atom ::= | '(' Expr as v ')' [':' Type as ty]
         | Num as v  [':' Type as ty]
         | Sym as v  [':' Type as ty]
         | Str as v  [':' Type as ty]
         | Lam as v  [':' Type as ty]
         rewrite Annotate(v, ty) if ty else v


Type    ::=
            | Type as head '->' as fn Type as tail
            | Type as head '*'  as tp Type as tail
            | '(' Type as nested ')'
            | Name as name ['of' Type as args]
            | SingleQuote Name as undecided
            rewrite

                TypeFunction(head, tail) if fn else\
                TypeJoin(head, tail) if tp else\
                nested if nested else\
                TypeInduct(name.value, args) if args else\
                TypeSym(name.value) if name else\
                TypeSlot(undecided.value)


TypeDef ::= 'type' Name as name ['of' Type as induct_arg] '=' Type as expand
            rewrite
                TypeDef(TypeInduct(name.value, induct_arg), expand) \
                if induct_arg else TypeAbbr(name.value, expand)

Grammar  ::= (Expr [';'])+ as seq
            rewrite
                Stmts(tuple(e for e in seq if Eq in e.__class__.__mro__))
